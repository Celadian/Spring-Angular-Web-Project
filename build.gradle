plugins {
	id 'org.springframework.boot' version '2.5.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'com.github.node-gradle.node' version '3.1.0'
}

ext {
	angularAppName = "dtr-angular-ui"
	angularDir = "$projectDir/$angularAppName"
}

group = 'com.dtr-band'
version = '0.0.1-SNAPSHOT'


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

node {
	nodeModulesDir = file(angularDir)
	download = true
	version = '14.16.0'
	npmVersion = '7.19.0'
}

task compileAngular(type: NpmTask, dependsOn: npmInstall) {
	def inputFiles = project.fileTree(dir: "${angularDir}", exclude: "**/karma-test-results.xml")
	inputs.files(inputFiles)

	def outputFile = "${angularDir}/dist"
	outputs.dir outputFile

	group 'build'
	description = 'Compile client side assets for production'
	args = ['run', 'build']
}

task copyAngular(type: Copy, dependsOn: compileAngular) {
	def inputFiles = project.fileTree(dir: "${angularDir}/dist/${angularAppName}")
	inputs.files(inputFiles)

	def outputFile = "${projectDir}/src/main/resources/static/"
	outputs.dir outputFile

	from "${angularDir}/dist/${angularAppName}"
	into "${projectDir}/src/main/resources/static"
}

tasks.processResources.dependsOn(copyAngular)

task testAngular(type: NpmTask, dependsOn: npmInstall) {
	def inputFiles = project.fileTree(dir: "${angularDir}", exclude: "**/karma-test-results.xml")
	inputs.files(inputFiles)

	def outputFile = "${angularDir}/coverage/"
	outputs.dir outputFile

	group 'verification'
	description = 'run ng test'
	args = ['run', 'test']
}

tasks.check.dependsOn(testAngular)

